Raspored:
    ((!Semestar /\w+|./)*
     semestri*=Semestar)*;

Semestar:
    semestar=INT 'Semestar'
    program=StudijskiProgram
    termini_po_danima+=TerminiZaDan;

TerminiZaDan:
    (!DanIliSemestar /\w+|./)* dan=Dan Heder
    (!Termin /\w+|./)*
    termin+=Termin;

StudijskiProgram[noskipws]: (!DanIliSemestar /\w+|\n|./)+;

DanIliSemestar:
    Dan | INT 'Semestar';

Heder:
    'Datum'? 'Grupa-e' 'Od' 'Do' 'Učionica' 'Vrsta nast.' 'Naziv Predmeta' 'Izvođač'
    (/\s*/ (!(Grupa|Datum) /[^\n]/)*)  // Pokupi izvođača koji je ispao iz linije
;

Dan[skipws]:
    'P' 'O' 'N' 'E' 'D' 'E' 'L' 'J' 'A' 'K'
    | 'U' 'T' 'O' 'R' 'A' 'K'
    | 'S' 'R' 'E' 'D' 'A'
    | 'Č' 'E' 'T' 'V' 'R' 'T' 'A' 'K'
    | 'P' 'E' 'T' 'A' 'K'
    | 'S' 'U' 'B' 'O' 'T' 'A'
    | 'B' 'L' 'O' 'K' 'N' 'A' 'S' 'T' 'A' 'V' 'A';

Termin:
    (datum=Datum)?  // Ako imamo datum u pitanju je blok nastava
    grupa=Grupa
    od=Vreme do=Vreme
    učionica=Učionica
    vrsta=Vrsta
    predmet=Predmet;

Grupa: /(\(|\)|\w|,|-)+/ /\s*/- &Vreme;
Datum: /\d{2}\.\d{2}\.\d{4}/;
Vreme: /\d{2}:\d{2}/;
Učionica: (!Vrsta /\w+|./)+;
Vrsta: 'rač.vežbe'
     | 'aud.vežbe'
     | 'lab.vežbe'
     | 'Pred.';
Predmet[noskipws]: /\s*/- (!KrajTermina /\w+|\n|./)+;
KrajTermina: Grupa | Dan | Datum | 'Rasporedi i realizacija' | 'Predmeti za koje nastavu';
